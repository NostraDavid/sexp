# note that the order of the rules is based on dependencies, instead of the
# original order from the RFC. I've also replaced all hex symbols with
# variables. All variables have been lowercased as well, as I am using this as a
# reference for my implementation.
# ALSO NOTE THAT ABNF MAY BE REVERSED ORDER - I'M USED TO DEFINING THE SMALL
# THINGS FIRST, THEN THE DERIVED THINGS LATER... NOT THE OTHER WAY AROUND
sexp           =  whitespace* value whitespace*
value          =  string / ("(" (value / whitespace)* ")")
string         =  display? simple_string
display        =  "[" whitespace* simple_string whitespace* "]" whitespace*
simple_string  =  verbatim / quoted_string / token / hexadecimal / base_64
base_64        =  decimal? "|" whitespace* base_64_chars* base_64_end? "|"
base_64_end    =  (base_64_chars / ((base_64_char whitespace*){3} ("=" whitespace*)?) / ((base_64_char whitespace*){2} ("=" whitespace*){,2}))
base_64_chars  =  (base_64_char whitespace*){4}
base_64_char   =  ~"[a-zA-Z0-9+/]"
hexadecimal    =  decimal? "#" whitespace* hexadecimals* "#"
hexadecimals   =  (hexdig whitespace*){2}
token          =  (alpha / simple_punc) (alpha / digit / simple_punc)*
simple_punc    =  ~"[-./_:*+=]"
quoted_string  =  decimal? dquote (printable / escaped)* dquote
escaped        =  backslash ( "?" / "a" / "b" / "f" / "n" / "r" / "t" / "v" / dquote / quote / backslash / (zero_to_seven{3}) / (x hexdig{2}) / cr / lf / (cr lf) / (lf cr))
x              =  "x"
zero_to_seven  =  ~"[0-7]"
quote          =  "'"
backslash      =  "\\"
printable      =  ~"[\x20-\x21\x23-\x5B\x5D-\x7E]"
verbatim       =  decimal ":" octet*
decimal        =  "0" / (~"[1-9]" digit*)
whitespace     =  sp / htab / vtab / cr / lf / ff
ff             =  "\f"  # form feed
vtab           =  "\v"  # vertical tab
dquote         =  "\""  # double quote
octet          =  ~"[\s\S]"  # Any character, including newlines
hexdig         =  ~"[0-9a-fA-F]"  # 0-9 / A-F / a-f
alpha          =  ~"[a-zA-Z]"  # A-Z / a-z
digit          =  ~"[0-9]"  # 0-9
lf             =  "\n"  # line feed
cr             =  "\r"  # carriage return
htab           =  "\t"  # horizontal tab
sp             =  " "   # space
